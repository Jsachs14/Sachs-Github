class Paddle {

    field int x,y,width,height,dir;

    constructor Paddle new(int Ax, int Ay, int Awidth, int Aheight) {
        let x = Ax;
        let y = Ay;
        let width = Awidth;
        let height = Aheight;
        let dir = 2;
        return this;
    }





    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    method void show() {
        do Screen.setColor(true);
        do Screen.drawRectangle(x,y,x+width,y+height);
        return;
    }

    method void hide() {
        do Screen.setColor(false);
        do Screen.drawRectangle(x,y,x+width,y+height);
        return;
    }


    method void setDir(int Adir) {
        let dir = Adir;
        return;
    }

    method int getDir() {
        return dir;
    }

    method int getLeft() {
        return x;
    }

    method int getRight() {
        return x+width;
    }

    method int getTop() {
        return y;
    }

    method int getBottom() {
        return y+height;
    }

    method void setWidth(int Awidth) {
        do hide();
        let width = Awidth;
        do show();
        return;
    }

    method void move() {
        //move left
        //dir = 1

        if (dir = 1) {
            let x = x-4;
            if (x < 0) {
                let x = 0;
            }

            //destroy the old paddle
            do Screen.setColor(false);
            do Screen.drawRectangle(x+width+1,y,x+width+4,y+height);
            //draw the new paddle
            do Screen.setColor(true);
            do Screen.drawRectangle(x,y,x+3,y+height);
        }
        else {
            //move right

            let x = x+4;
            if ((x+width) > 511) {
                let x = 511-width;
            }

            //destroy the old paddle
            do Screen.setColor(false);
            do Screen.drawRectangle(x-4,y,x-1,y+height);
            //draw the new paddle
            do Screen.setColor(true);
            do Screen.drawRectangle(x+width-3,y,x+width,y+height);

        }

        return;
    }






}