class Paddle {
    field int x,y,height,width,direction;


    //0 = left 1 = right
    constructor Paddle new(int Ax, int Ay, int Awidth, int Aheight) {
        let x = Ax;
        let y = Ay;
        let width = Awidth;
        let height = Aheight;
        let direction = 1;

        do show();

        return this;
    }

    //destructor
    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    method void show() {
        do Screen.setColor(true);
        do draw();
        return;
    }

    method void hide() {
        do Screen.setColor(false);
        do draw();
        return;
    }

    method void draw() {
        do Screen.drawRectangle(x,y,x+width,y+height);
        return;
    }

    method void setDir(int dir) {
        let direction = dir;
        return;
    }

    //getter methods

    method int getXleft() {
        return x;
    }

    method int getXright() {
        return x+width;
    }

    method void setWidth(int Awidth) {
        do hide();
        let width = Awidth;
        do show();
        return;
    }

    method void movement() {
        if (direction=1) {
            let x = x-4;
            if (x<0) {
                let x = 0;
            }
            do Screen.setColor(false);
            do Screen.drawRectangle(x+width+1,y,x+width+4,y+height);
            do Screen.setColor(true);
            do Screen.drawRectangle(x,y,x+3,y+height);
        }
        else {
            let x = x+4;
            if ((x+width)>511) {
                let x = 511-width;
            }
            do Screen.setColor(false);
            do Screen.drawRectangle(x-4,y,x-1,y+height);
            do Screen.setColor(true);
            do Screen.drawRectangle((x + width) - 3, y, x + width, y + height);

        }
        return;
    }

}